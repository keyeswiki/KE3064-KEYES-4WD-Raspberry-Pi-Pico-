# 第09课 超声波避障智能小车
![Img](./media/f4007b54933a1c0bdf054d62a84bda9b.png)

## 1. 实验说明：                                                                                
在上个项目中，我们制作了一个超声波跟随智能车。实际上，利用同样的硬件，同样的接线方法，我们只需要更改一个测试代码就可以将跟随智能车变为避障智能车。超声波避障智能车，就是通过超声波传感器检测前方障碍物的距离，然后舵机云台转动检测到左右两边的距离，然后根据这一个数据控制四个电机的转动，从而控制智能车的运动状态，从而实现避障。
我们来看看下面的程序流程图就很清晰了：

## 2. 实验流程图： 
![Img](./media/4f4a7c1ea0c756e463359b730db03eee.png)

## 3. 添加库文件：
首先我们先确保安装了库文件，不然代码会编译出错，这里我们用到库文件“<span style="color: rgb(255, 76, 65);">MecanumCar_v2</span>”和“<span style="color: rgb(255, 76, 65);">Servo</span>”，如何安装库文件请参考Arduino C 教程中的“<span style="color: rgb(0, 209, 0);">开发环境配置</span>”文件。如果库文件已经添加好了，就跳过这一步骤。
![Img](./media/b1b60f9edaa8f26ecf2534f1e0be73aa.png)

## 4. 实验代码：

```
/*
  KEYES 4WD Raspberry Pi Pico 麦克纳姆轮智能小车
  第9课
  超声波避障智能小车
*/
#include "MecanumCar_v2.h"
#include "Servo.h"

mecanumCar mecanumCar(20, 21);  //sda-->20,scl-->21
Servo myservo;    //定义一个舵机类实例

/*******超声波传感器接口*****/
#define EchoPin  4  //ECHO 引脚连接到 GPIO4
#define TrigPin  3  //TRIG 引脚连接到 GPIO3

int distance_M, distance_L, distance_R;

void setup() {
  myservo.attach(2);    //舵机引脚连接到GP2
  pinMode(EchoPin, INPUT);    //ECHO引脚设置为输入模式
  pinMode(TrigPin, OUTPUT);   //TRIG引脚设置为输出模式
  myservo.write(90); //转动到90度
  delay(100);
}

void loop() {
  distance_M = get_distance();  //获取距离保存在distance变量
  if (distance_M < 15) {//当测到前方的距离小于15cm时
    mecanumCar.Stop();  //机器人停止
    delay(500); //延时500ms
    myservo.write(180);  //超声波云台左转
    delay(500); //延时500ms
    distance_L = get_distance();  //把超声波测到左边的距离赋给变量 distance_L
    delay(100); //稳定读取值
    myservo.write(0); //超声波云台右转
    delay(500); //延时500ms
    distance_R = get_distance(); //把超声波测到右边的距离赋给变量 distance_R
    delay(100); //稳定读取值

    myservo.write(90);  //回到90度位置
    delay(500);
    if (distance_L > distance_R) { //左边的距离大于右边时
      mecanumCar.Turn_Left();  //机器人左转
      delay(300);  //左转700ms
    } else {
      mecanumCar.Turn_Right(); //机器人右转
      delay(300);
    }
  }
  else { //如果测到前方的距离>=15cm时，机器人前进
    mecanumCar.Advance(); //前进
  }
}

/*********************超声波测距*******************************/
int get_distance(void) {    //超声波测距
  int dis;
  digitalWrite(TrigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(TrigPin, HIGH); //给TRIG至少10us的高电平以触发
  delayMicroseconds(10);
  digitalWrite(TrigPin, LOW);
  dis = pulseIn(EchoPin, HIGH) / 58.2; //计算出距离
  delay(30);
  return dis;
}
```

## 5. 实验结果：
安上电池，将电机驱动底板上的电源拨码开关拨到ON一端，开启电源。选择好开发板(Raspberry Pi Pico)和端口(COMx)，编译上传代码完成后，小车就能自动避障了，注意这里速度不要调得太大。当小车行驶过程中前方遇到障碍物，小车将停止，然后舵机云台转动到左边，测下左边障碍物的距离；然后舵机云台转动到右边，测下右边障碍物的距离，然后判断左边与右边障碍物的距离，哪边距离更远，小车就往那边转弯，然后继续行驶。

## 6. 代码说明: 
| myservo.write(90); | 让舵机转动到90度位置 |
| :--: | :--: |
| int distance = get_distance();  | 定义一个整数变量用来保存测得的距离，后面根据这个距离来控制小车行驶 |
|![Img](./media/4e85af3b3e43c183d50625b0c902bfdf.png)| 超声波转动到左边，测左边的距离，保存 |
|![Img](./media/e8053955b2e9b79cccb80d3b7d93be87.png)| 超声波转动到右边，测右边的距离，保存 |
| if (distance_L > distance_R) |判断左右两边距离，谁更大 |
|![Img](./media/d933cfe5da54cd6637b1e1fcfb5f9070.png)| 如果前方距离大于等于15cm时，小车继续直行 |










